apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.devtools.ksp'

def rootCMakeLists = "../../CMakeLists.txt"
def rootCMakeListsContent = file(rootCMakeLists).text
static def grepVersionComponent(content, varname) {
    def match = content =~ /set\($varname ([0-9]+)\)/
    if(!match.find())
        throw new GradleException("Failed to find $varname in CMakeLists.txt")
    return match.group(1)
}
def chiakiVersionMajor = grepVersionComponent(rootCMakeListsContent, "CHIAKI_VERSION_MAJOR")
def chiakiVersionMinor = grepVersionComponent(rootCMakeListsContent, "CHIAKI_VERSION_MINOR")
def chiakiVersionPatch = grepVersionComponent(rootCMakeListsContent, "CHIAKI_VERSION_PATCH")
def chiakiVersion = "$chiakiVersionMajor.$chiakiVersionMinor.$chiakiVersionPatch"
println("Determined Chiaki Version: $chiakiVersion")

android {
    compileSdk 34
    buildToolsVersion "34.0.0"
    namespace "com.metallic.chiaki"
    defaultConfig {
        applicationId "com.metallic.chiaki"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 11
        versionName chiakiVersion
        externalNativeBuild {
            cmake {
                arguments "-DCHIAKI_ENABLE_TESTS=OFF",
                        "-DCHIAKI_ENABLE_CLI=OFF",
                        "-DCHIAKI_ENABLE_GUI=OFF",
                        "-DCHIAKI_ENABLE_SETSU=OFF",
                        "-DCHIAKI_ENABLE_ANDROID=ON",
                        "-DCHIAKI_ENABLE_FFMPEG_DECODER=OFF",
                        "-DCHIAKI_ENABLE_PI_DECODER=OFF",
                        "-DCHIAKI_LIB_ENABLE_OPUS=OFF",
                        "-DCHIAKI_ENABLE_STEAM_SHORTCUT=OFF",
                        "-DCHIAKI_ENABLE_STEAMDECK_NATIVE=OFF",
                        "-DCHIAKI_LIB_ENABLE_MBEDTLS=ON",
                        "-DCHIAKI_LIB_MBEDTLS_EXTERNAL_PROJECT=ON"
            }
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    externalNativeBuild {
        cmake {
            version "3.18.1+"
            path rootCMakeLists
        }
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


    Properties properties = new Properties()
    def propertiesFile = file("../local.properties")
    if (propertiesFile.exists()) {
        properties.load(propertiesFile.newDataInputStream())
    }

    // Support environment variables for CI/CD (GitHub Actions)
    def keystorePath = System.getenv("KEYSTORE_PATH") ?: properties.get("chiakiKeystore")
    def keystorePassword = System.getenv("KEYSTORE_PASSWORD") ?: properties.get("chiakiKeystorePW")
    def keyAlias = System.getenv("KEY_ALIAS") ?: properties.get("chiakiKeyAlias")
    def keyPassword = System.getenv("KEY_PASSWORD") ?: properties.get("chiakiKeyPW")

	if(keystorePath && keystorePassword && keyAlias && keyPassword) {
        println("Enabling Signing (Local or CI)")
        buildTypes.release.signingConfig = signingConfigs.release
        buildTypes.debug.signingConfig = signingConfigs.release
        signingConfigs.release.storeFile = file(keystorePath)
        signingConfigs.release.storePassword = keystorePassword
        signingConfigs.release.keyAlias = keyAlias
        signingConfigs.release.keyPassword = keyPassword
    } else {
        println("Signing not enabled - no keystore configuration found")
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += "-opt-in=kotlin.ExperimentalUnsignedTypes"
        jvmTarget = "17"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-reactivestreams:2.2.0'
    implementation "io.reactivex.rxjava2:rxjava:2.2.20"
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    def room_version = "2.5.2"
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation "com.squareup.moshi:moshi:1.9.2"
    ksp "com.squareup.moshi:moshi-kotlin-codegen:1.9.2"
}
